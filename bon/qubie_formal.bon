static_diagram QUBIE_TOPLEVEL
component 
	cluster MAIN
	component 
	    class QUBIE_STATE
	    inherit VALUE
	    invariant
	      Current member_of {powered_on, booting, running, stopped, powered_off}
	    end
	    deferred class QUBIE_INTERFACE
	    feature
			--the following booleans represent possible qubie states
			powered_on : BOOLEAN
				ensure Result = (state = powered_on) end
			booting : BOOLEAN 
				ensure Result = (state = booting) end
			running : BOOLEAN 
				ensure Result = (state = running) end
			stopped : BOOLEAN --qubie has run and stopped
				ensure Result = (state = stopped) end
			powered_off : BOOLEAN
				ensure Result = (state = powered_off) end
			state : QUBIE_STATE
			deferred log : LOG
			deferred qubie : QUBIE_INTERFACE 
			deferred action_published : BOOLEAN
				-> entry : ANY
			
			power_on
				ensure
					state = powered_on;
					action_published(state)
				end
			start_booting 
				require
					powered_on; 
					--power_on or stopped; --currently soft reset is not allowed
				ensure
					state = booting;
					action_published(state);	
				end
			start_running -- : Void 
				require
					booting
				ensure
					state = running;
					action_published(state);
				end
			stop_running 
				require running
				ensure
					state = stopped;
					action_published(state);
				end
			power_off
				ensure
					state = powered_off;
					action_published(state);
				end
			deferred publish_action	
				-> the_state : QUBIE_STATE
				ensure action_published(the_state); end
				
			update_state	--mapping to allow external acces to change states
				-> the_state : QUBIE_STATE
				require
					the_state member_of legal_update_state;
				ensure
					the_state = qubie.state;
--					(the_state = booting) -> qubie.start_booting;
--					(the_state = running) -> qubie.start_running;
--					(the_state = stopped) -> qubie.stop_running;
--					(the_state = powered_off) -> qubie.power_off;
				end
				
	    	legal_update_states : SET[QUBIE_STATE]
	    		ensure
	    			Result = {booting, running, stopped, powered_off};
	    		end
	    end

		effective class QUBIE
		inherit QUBIE_INTERFACE
		feature
			--TODO do I need to define the ">" operator for QUBIE_STATE?
			observations : OBSERVATIONS
				require state > booting end	
			wifi_monitor : WIFI_MONITOR
				require state > booting end
			bt_communicator : BLUETOOTH_COMMUNICATOR
				require state > booting end
			effective log : LOG
			initialized : BOOLEAN --TODO get consensus on syntax
				ensure 
					Result = log.empty and observations.empty;
				end
			effective action_published : BOOLEAN
				-> the_state : QUBIE_STATE
				ensure
					Result = log.logged(Current) and bt_communicator.action_published(the_state);		
				end
			effective publish_action
				-> the_state : QUBIE_STATE
				ensure
					action_published(the_state);
				end
				

			redefined legal_update_states : SET[QUBIE_STATE]
				ensure Result = {stopped, powered_off}; end
			power_on_boot_and_run
				require
					powered_on;
				ensure
					running;
				end
			record_observation 
				-> contact_record : CONTACT_RECORD
				ensure 
					delta {observations, log}; --if observations is a sequence/list we could limit to observations(size)
					observations.contains(contact_record);
					log.logged(Current)
				end

--		constraint
--			old(stopped) implies stopped	
		end	
	end
end


static_diagram QUBIE_MONITOR
component
	class MONITOR
	feature 
		wifi_booted : BOOLEAN
		wifi_running : BOOLEAN
		auto_hopping : BOOLEAN
		keyed_hash : KEYED_HASH
		frequency_range : SET[FREQUENCY]
		frequency : FREQUENCY
		qubie : QUBIE
		
		boot_wifi
			require 
				not booted;
			ensure
				booted;
				keyed_hash.set;
				qubie.log.logged(Current);
			end
		start_wifi
			require
				booted;
				not running;
			ensure
				--delta {Current, qubie.observations, qubie.log};
				running;
				qubie.log.logged(Current);
			end
		stop_wifi
			require
				running;
			ensure
				not running; 
				qubie.log.logged(Current);
			end
		set_frequency 
			-> the_frequency : FREQUENCY
			ensure 
				delta {frequency, qubie.log};
				frequency=the_frequency; 
				qubie.log.logged(Current);	
			end
		set_auto_hopping 
			-> the_truth_value : BOOLEAN
			ensure 
				delta {auto_hopping, qubie.log};
				auto_hopping=the_truth_value; 
				qubie.log.logged(Current);	
			end
		report_detected_device
			-> the_mac_address : VALUE
			-> the_signal_strength : VALUE
			ensure
				delta {qubie.observations, qubie.log};
				qubie.observations.contains(CONTACT_RECORD.make(keyed_hash, the_mac_address, the_signal_strength));
				qubie.log.logged(Current);	
			end
		--TODO the keyed hash is never changed after running
	end
	
	class FREQUENCY
	inherit VALUE
	end
	class WIFI_MONITOR
	inherit MONITOR
	end
end
	


static_diagram QUBIE_COMMUNICATOR
component
	deferred class COMMUNICATOR
	inherit QUBIE_INTERFACE
	feature
		effective qubie : QUBIE
		effective log : LOG
			ensure Result = qubie.log; end
		qubie_state : QUBIE_STATE
			ensure
				Result=qubie.state;
			end
		redefined update_qubie_state {^QUBIE_INTERFACE.update_state}
			-> the_state : QUBIE_STATE
	
		redefined legal_update_states : SET[QUBIE_STATE]
			ensure Result = {stopped, powered_off}; end

	end
	effective class BLUETOOTH_COMMUNICATOR
	inherit COMMUNICATOR
	feature
		subscribed : BOOLEAN
		bt_client : BLUETOOTH_CLIENT
			require subscribed; end
		effective action_published : BOOLEAN
			-> the_state : QUBIE_STATE
			ensure
				--publish to all existing clients;
				subscribed -> bt_client.received(the_state);
			end
		subscribe
			-> the_bluetooth_client : BLUETOOTH_CLIENT
			require
				not subscribed;
			ensure
				delta {bt_client, subscribed}; --TODO? is this accurate taking into account that the bt_client, once subscribed, can modify qubie states etc.?
				bt_client=the_bluetooth_client;
				subscribed;
			end
		unsubscribe
			require
				subscribed;
			ensure
				delta {bt_client, subscribed};
				not subscribed;
			end
		effective publish_action
			-> the_state : QUBIE_STATE
			ensure 
				action_published(the_state);
			end
			--TODO ensure if subscribed -> *notify client* end
	end
	--TODO class PHYSICAL_COMMUNICATOR inherit COMMUNICATOR for buttons+leds
	--TODO TOUCH_SCREEN inherit COMMUNICATOR
	class BLUETOOTH_CLIENT 
	feature
		bt_communicator : BLUETOOTH_COMMUNICATOR
		qubie_state : QUBIE_STATE
		published : BOOLEAN
			-> the_state : QUBIE_STATE
			ensure qubie_state = the_state; end
		publish_from_bt_communicator
			-> the_state : QUBIE_STATE
			ensure 
				delta Current;
				qubie_state = the_state;
				published(the_state); 
			end
		update_qubie_state
			the_state : QUBIE_STATE
			--require
				--bt_communicator.bt_client = Current; --TODO? is this obvious?
			ensure 
				bt_communicator.qubie_state = the_state;
				published(the_state); 
			end
	end
end	

	
static_diagram QUBIE_KEYED_HASH
component
	class HASH reused
	class KEYED_HASH
	feature
		set : BOOLEAN --write-once
		create_random_key : VALUE
		set_key
			-> the_key : VALUE
			require
				not set;
			ensure
				delta {set, hash, key};
				key = the_key;
				set;
			end
		hashed_string : VALUE
			->the_string : VALUE
			require set;
			ensure
				Result = hash.hash(the_string);
				--Return the keyed hash of the string
			end
--	constraint
--		old.set implies set
	--invariant
		--for_all a,b:STRING it_holds ((hashed_string(a)=hashed_string(b))=(a=b) )
		--exists a:STRING such_that (a /= hashed_string(a))
--	end
--	feature NONE
		key : VALUE --write-once
		hash : HASH --we know that hash is functional
	end
end
	
static_diagram QUBIE_OBSERVATIONS
component
	class DEVICE_IDENTIFIER --immutable
	feature
		encrypted : BOOLEAN
		identifier_string : VALUE
	end
	class TIME reused
	class CONTACT_RECORD --immutable
	feature
		device_id : DEVICE_IDENTIFIER
		contact_time : TIME
		rssi : VALUE
		frequency : FREQUNCY
	end
	class OBSERVATIONS inherit SET[CONTACT_RECORD]  --TODO make this a sequence, or perhaps list
	feature
		size : NATURAL
		empty : BOOLEAN
			ensure Result = (size = 0); end
		contains : BOOLEAN
			-> the_contact_record : CONTACT_RECORD
			ensure 
				the_contact_record member_of Current; 
			end
		add_contact_record
			-> the_contact_record : CONTACT_RECORD
			ensure
				size = old size + 1;
				contains(the_contact_record);
			end
	end
end


static_diagram QUBIE_LOGGER
component 
	class LOG_ENTRY --immutable
	feature
		message : STRING
		time : STRING
	end
	class LOG persistent
	inherit LIST[LOG_ENTRY] 
	feature
--		entry++ : LOG_ENTRY {^LIST.element} --rename element as entry
		current_time : VALUE
		empty : BOOLEAN
			ensure Result = (size = 0); end
		logged : BOOLEAN
			-> entry : ANY
			ensure
				delta element(size);
			end			
		log
			-> entry : ANY
			ensure
				logged(entry);
			end
	end
end
	
	
	



	
	